/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <csx>
#include <nvault>

// Count knife kills and store it on Counter-Strike 1.6 AMXX server

#define PLUGIN "Count Knife Kills"
#define VERSION "1.0"
#define AUTHOR "Sebastian Bort"

#define MINIMUM_PLAYERS 6

new knife_counter[33];
new file_handler;

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);
	
	register_clcmd("say /knifekills", "display_knife_kills");	
	
	file_handler = nvault_open("knife_kills");
	if(file_handler == INVALID_HANDLE)
	    set_fail_state("Error opening nVault");  
}

public client_connect(id) {
		
	new authid[33], file_data[6], timestamp, existing_user;
	get_user_authid(id, authid, sizeof authid  - 1);
	
	existing_user = nvault_lookup(file_handler, authid, file_data, sizeof file_data - 1, timestamp);
	if(!existing_user) {
		knife_counter[id] = 0;
	}
	else {
		knife_counter[id] = str_to_num(file_data);
	}
}


public client_disconnect(id) {
		
	new authid[33], file_data[6];
	get_user_authid(id, authid, sizeof authid  - 1);
	
	num_to_str(knife_counter[id], file_data, sizeof file_data - 1);
	nvault_set(file_handler, authid, file_data);  
}

public client_death(killer, victim, weapon, hitplace, teamkill) {
	
	if(weapon != CSW_KNIFE) 
		return;
	
	if(get_playersnum() < MINIMUM_PLAYERS) 
		return;
		
	knife_counter[killer]++;
}

public display_knife_kills(id) {
	
	client_print(id, print_chat, "Your knife kills: %d times", knife_counter[id]);
	return PLUGIN_HANDLED
}

public plugin_end() {
	nvault_close(file_handler);
}